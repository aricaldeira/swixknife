^0 zero
1 un
2 doys
3 treys
4 kwatru
5 sinku

10 sêys
20 duseys
30 treseys
40 quaseys
50 kinseys
(\d)(\d) $(\10) i $2

100 zêyn
110 zêyn i sêys
200 duzeyn
210 duzeyn i sêys
300 trezeyn
310 trezeyn i sêys
400 kwazeyn
410 kwazeyn i sêys
500 kinzeyn
510 kinzeyn i sêys
(\d)0(\d) $(\100) i $2
(\d)(\d)0 $(\100) i $(\20)
(\d)(\d\d) $(\100) $2

1000 sêys zêyn
100(\d) sêys zêyn i $1
10(\d)0 sêys zêyn i $(\10)
10(\d\d) sêys zêyn $1

(\d\d)00 $1 zêyn
(\d\d)10 $1 zêyn i sêys
(\d\d)0(\d) $1 zêyn i $2
(\d\d)(\d)0 $1 zêyn i $(\20)
(\d\d)(\d\d) $1 zêyn $2

#
# Nas règras abaixu, u númeru ki entra
# è us prósimus 4 díjitus depoys di un
# grupu
#
:0+
:0*[12345] " i "
:0*[12345]0+ " i "
:0*[12345]00+ " i "
:0*[12345][012345]00+ " i "
:\d+ ", "


pl:1	awn			# milyawn
pl:.*	oyns		# milyoyns

1(\d\d\d\d) ziw$(:\1)$1
(\d{1,4})(\d\d\d\d) $1 ziw$(:\2)$2

(\d{1,4})(\d{8})  $1 zily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{12}) $1 duzily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{16}) $1 trezily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{20}) $1 kwazily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{24}) $1 kinly$(pl:\1)$(:\2)$2
(\d{1,4})(\d{28}) $1 sezily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{32}) $1 seziunzily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{36}) $1 seziduzily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{40}) $1 sezitrezily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{44}) $1 sezikwazily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{48}) $1 sezikinzily$(pl:\1)$(:\2)$2
(\d{1,4})(\d{52}) $1 dusezily$(pl:\1)$(:\2)$2

#
# Númerus negativus
#
[-−](\d\d*) menus |$1

#
# Frasoyns
#
#([-−]?\d+)[,.] $1| pontu
([-−]?\d+)[,.] $1| vírgula
([-−]?\d+[.,])([^0]\d{1,4}) $1| |$2
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7 |$8
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7 |$8 |$9
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7 |$8 |$9 |$10
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7 |$8 |$9 |$10 |$11
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)(\d)" |$1 |$2| |$3| |$4 |$5 |$6 |$7 |$8 |$9 |$10 |$11 |$12
([-−]?\d+[.,]\d*)(\d) $1| |$2

### UNITS_AND_PREFIXES ###

== feminine ==

([-−]?\d+) $(feminine $1)

(.*)un \1uma
(.*)doys(.*)	$(feminine \1duas\2)
(.*) \1

== masculine ==

([-−]?\d+) $1

== ordinal(-masculine)? ==

0 zerézimu
1 primeru
2 segundu
3 terseru
4 kwartu
5 kintu
10 sestu
20 dusexto
30 tresestu
40 kwasestu
50 kinsestu
(\d)(\d) $(ordinal \20) $(ordinal \3)
100 zeỹézimu
200 duzeỹézimu
300 trezeỹézimu
400 kwazeỹézimu
500 kinzeỹézimu
(\d)(\d\d) $(ordinal \200) $(ordinal \3)
1(\d{4}) zilézimu[ $(ordinal \2)]
(\d)(\d{4}) $2 zilézimu[ $(ordinal \3)]
1(\d{8}) zilyonézimu[ $(ordinal \2)]
(\d{1,7})(\d{8}) $2 zilyonézimu[ $(ordinal \3)]
1(\d{12}) duzilyonézimu[ $(ordinal \2)]
(\d{1,4})(\d{12}) $2 duzilyonézimu[ $(ordinal \3)]

== ordinal-feminine ==

([-−]?\d+) $(ordinal-feminine $(ordinal-masculine \1))
(.*)u\b(.*)  $(ordinal-feminine \1a\2)
(.*)   \1

== (ordinal)-number(-feminine|-masculine)? ==

([-−]?\d+) \3$(ordinal-number $(\1\2 \3))
.*a ᵃ̱
.*u ᵘ̱

== help ==

"" $(1)|, $(2), $(3)\n$(\0 feminine)$(\0 masculine)$(\0 ordinal-feminine)$(\0 ordinal-masculine)$(\0 ordinal-number-feminine)$(\0 ordinal-number-masculine)
(feminine|masculine|ordinal(-number)?(-feminine|-masculine)?) \1: $(\1 1), $(\1 2), $(\1 3)\n
