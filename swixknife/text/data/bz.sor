\+ máys
\- menus
− menus
² aw kwadradu
³ aw kubu
\^ elevadu a
\*\* elevadu a
\* vezis
× vezis
/ divididu pur
÷ divididu pur
([-−]?1)[/⁄](\d+) $1 $(fractional \\2)
([-−]?\d+)[/⁄](\d+) $1 $(fractional-plural \\2)
([-−]?\d+|[-−]?[πτφ])[/⁄]([-−]?\d+|[-−]?[πτφ]) $1 sobri $2
⁄ sobri
[.,]{2} periwdu
e e
π pi
τ taw
φ fi
\( abri parêntezis
\) fèxa parêntezis

^0 zèro
1 un
2 doys
3 treys
4 kwatru
5 sinku
10 sêys
11 sèti
12 oytu
13 nòvi
14 dèys
15 onzi
20 dozi
# 20 duseys

30 treseys
40 quaseys
50 kinseys
(\d)(\d) $(\10) i $2

100 nifi
200 dunifi
300 trenifi
400 kwatrunifi
500 kinifi

(\d)(0\d) $(\100) i $2
(\d)(1\d) $(\100) i $2
(\d)(\d0) $(\100) i $2
(\d)(\d\d) $(\100) $2

1000 arda
100(\d) arda i $1
10(1\d) arda i $1
10(\d0) arda i $1
1(\d00) arda i $1
1(\d\d\d) arda $1

(\d{1,3})000 $1 arda
(\d{1,3})00(\d) $1 arda i $2
(\d{1,3})0(1\d) $1 arda i $2
(\d{1,3})0(\d0) $1 arda i $2
(\d{1,3})(\d00) $1 arda i $2
(\d{1,3})(\d\d\d) $1 arda, $2

(\d{1,6})000000 $1 xadara
(\d{1,6})00000(\d) $1 xadara i $2
(\d{1,6})0000(1\d) $1 xadara i $2
(\d{1,6})0000(\d0) $1 xadara i $2
(\d{1,6})000(\d00) $1 xadara i $2
(\d{1,6})00(\d000) $1 xadara i $2
(\d{1,6})0(\d0000) $1 xadara i $2
(\d{1,6})(\d00000) $1 xadara i $2
(\d{1,6})(\d{6}) $1 xadara, $2

#
# Nas règras abaixu, u númeru ki entra
# è us prósimus 3 díjitus depoys di un
# grupu
#
:0+
:0*[12345] " i "
:0*[12345]0+ " i "
:0*[12345]00+ " i "
:\d+ ", "

(1)(\d{12}) dixadara$(:\2)$2
(1)(\d{18}) trixadara$(:\2)$2
(1)(\d{24}) xarxadara$(:\2)$2
(1)(\d{30}) panxadara$(:\2)$2
(1)(\d{36}) xaxadara$(:\2)$2
(1)(\d{42}) xaekaxadara$(:\2)$2
(1)(\d{48}) xadixadara$(:\2)$2
(1)(\d{54}) xatrixadara$(:\2)$2
(1)(\d{60}) xaxarxadara$(:\2)$2
(1)(\d{66}) xapanxadara$(:\2)$2
(1)(\d{72}) dixaxadara$(:\2)$2

(\d{1,6})(\d{12}) $1 dixadara$(:\2)$2
(\d{1,6})(\d{18}) $1 trixadara$(:\2)$2
(\d{1,6})(\d{24}) $1 xarxadara$(:\2)$2
(\d{1,6})(\d{30}) $1 panxadara$(:\2)$2
(\d{1,6})(\d{36}) $1 xaxadara$(:\2)$2
(\d{1,6})(\d{42}) $1 xaekaxadara$(:\2)$2
(\d{1,6})(\d{48}) $1 xadixadara$(:\2)$2
(\d{1,6})(\d{54}) $1 xatrixadara$(:\2)$2
(\d{1,6})(\d{60}) $1 xaxarxadara$(:\2)$2
(\d{1,6})(\d{66}) $1 xapanxadara$(:\2)$2
(\d{1,6})(\d{72}) $1 dixaxadara$(:\2)$2

#
# Númerus negativus
#
[-−](\d\d*) menus |$1

#
# Frasoyns
#
#([-−]?\d+)[,.] $1| pontu
([-−]?\d+)[,.] $1| vírgula
([-−]?\d+[.,])([^0]\d{1,2}) $1| |$2
([-−]?\d+[.,]\d*)(\d) $1| |$2

### UNITS_AND_PREFIXES ###

== feminine ==

([-−]?\d+) $(feminine $1)

(.*)un \1uma
(.*)doys(.*)	$(feminine \1duas\2)
(.*) \1

== masculine ==

([-−]?\d+) $1

== ordinal(-masculine)? ==

0 zerézimu
1 primeru
2 segundu
3 terseru
4 kwartu
5 kintu
10 sestu
11 sétimu
12 oytavu
13 nonu
14 désimu
15 ônzimu
20 dôzimu
30 tresestu
40 kwasestu
50 kinsestu
(\d)(\d) $(ordinal \20) $(ordinal \3)
100 nifézimu
200 dunifézimu
300 trenifézimu
400 kwatrunifézimu
500 kinifézimu
(\d)(\d\d) $(ordinal \200) $(ordinal \3)
1(\d{3}) ardézimu[ $(ordinal \2)]
(\d{1,3})(\d{3}) $(ordinal \2) ardézimu[ $(ordinal \3)]
1(\d{6}) xadarézimu[ $(ordinal \2)]
(\d{1,5})(\d{6}) $(ordinal \2) xadarézimu[ $(ordinal \3)]
1(\d{12}) dixadarézimu[ $(ordinal \2)]
(\d{1,5})(\d{12}) $(ordinal \2) dixadarézimu[ $(ordinal \3)]

== ordinal-feminine ==

([-−]?\d+) $(ordinal-feminine $(ordinal-masculine \1))
(.*)u\b(.*)  $(ordinal-feminine \1a\2)
(.*)   \1

== (ordinal)-number(-feminine|-masculine)? ==

([-−]?\d+) \3$(ordinal-number $(\1\2 \3))
.*a ᵃ̱
.*u ᵘ̱

== fractional ==

2 meyu
3 tersu
4 kwartu
5 kintu
10 sestu
11 sétimu
12 oytavu
13 nonu
14 désimu
15 ônzimu
20 dusestu
30 tresestu
40 kwasestu
50 kinsestu
100 nifézimu
200 dunifézimu
300 trenifézimu
400 kwatrunifézimu
500 kinifézimu
1000 ardézimu
1000000 xadarézimu
(\d+) $(\\1) avus

== fractional-plural ==

2 meyus
3 tersus
4 kwartus
5 kintus
10 sestus
11 sétimus
12 oytavus
13 nonus
14 désimus
15 ônzimus
20 dusestus
30 tresestus
40 kwasestus
50 kinsestus
100 nifézimus
200 dunifézimus
300 trenifézimus
400 kwatrunifézimus
500 kinifézimus
1000 ardézimus
1000000 xadarézimus
(\d+) $(\\1) avus

== help ==

"" $(1)|, $(2), $(3)\n$(\0 feminine)$(\0 masculine)$(\0 ordinal-feminine)$(\0 ordinal-masculine)$(\0 ordinal-number-feminine)$(\0 ordinal-number-masculine)
(feminine|masculine|ordinal(-number)?(-feminine|-masculine)?) \1: $(\1 1), $(\1 2), $(\1 3)\n
