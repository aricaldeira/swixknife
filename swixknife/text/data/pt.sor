\+ mais
\- menos
− menos
\^ elevado a
\*\* elevado a
\* vezes
× vezes
/ dividido por
÷ dividido por
([-−]?1)[/⁄](\d+) $1 $(fractional \\2)
([-−]?\d+)[/⁄](\d+) $1 $(fractional-plural \\2)
([-−]?\d+|[-−]?[πτφ])[/⁄] $1 sobre
([-−]?\d+|[-−]?[πτφ])[/⁄]([-−]?\d+|[-−]?[πτφ]) $1 sobre $2
⁄ sobre
m[oó]d módulo
([-−]?\d+|[-−]?[πτφ])! fatorial de $1
[.,󱹮]{2}|[‥„󱹯] e se repete
e e
π pi
τ tau
φ fi
ln\((.*)\) logaritmo natural de $1
ln\((.*) logaritmo natural de $1
ln\( logaritmo natural de
lsez\((.*)\) logaritmo sezimal de $1
lsez\((.*) logaritmo sezimal de $1
lsez\( logaritmo sezimal de
ldec\((.*)\) logaritmo decimal de $1
ldec\((.*) logaritmo decimal de $1
ldec\( logaritmo decimal de
sqrt\((.*)\) raiz quadrada de $1
sqrt\((.*) raiz quadrada de $1
sqrt\( raiz quadrada de
cbrt\((.*)\) raiz cúbica de $1
cbrt\((.*) raiz cúbica de $1
cbrt\( raiz cúbica de
\u200b?² ao quadrado
\u200b?³ ao cubo
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?² $1 ao quadrado
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?³ $1 ao cubo
(?:s[ie]n|S[IE]N)\((.*)\) seno de $1
(?:s[ie]n|S[IE]N)\((.*) seno de $1
(?:s[ie]n|S[IE]N)\( seno de
(?:as[ie]n|AS[IE]N|arcs[ie]n|ARCS[IE]N)\((.*)\) arcosseno de $1
(?:as[ie]n|AS[IE]N|arcs[ie]n|ARCS[IE]N)\((.*) arcosseno de $1
(?:as[ie]n|AS[IE]N|arcs[ie]n|ARCS[IE]N)\( arcosseno de
(?:csc|CSC)\((.*)\) cossecante de $1
(?:csc|CSC)\((.*) cossecante de $1
(?:csc|CSC)\( cossecante de
(?:acsc|ACSC|arccsc|ARCCSC)\((.*)\) arcocossecante de $1
(?:acsc|ACSC|arccsc|ARCCSC)\((.*) arcocossecante de $1
(?:acsc|ACSC|arccsc|ARCCSC)\( arcocossecante de
(?:cos|COS)\((.*)\) cosseno de $1
(?:cos|COS)\((.*) cosseno de $1
(?:cos|COS)\( cosseno de
(?:acos|ACOS|arccos|ARCCOS)\((.*)\) arcocosseno de $1
(?:acos|ACOS|arccos|ARCCOS)\((.*) arcocosseno de $1
(?:acos|ACOS|arccos|ARCCOS)\( arcocosseno de
(?:sec|SEC)\((.*)\) secante de $1
(?:sec|SEC)\((.*) secante de $1
(?:sec|SEC)\( secante de
(?:asec|ASEC|arcsec|ARCSEC)\((.*)\) arcossecante de $1
(?:asec|ASEC|arcsec|ARCSEC)\((.*) arcossecante de $1
(?:asec|ASEC|arcsec|ARCSEC)\( arcossecante de
(?:tan|TAN)\((.*)\) tangente de $1
(?:tan|TAN)\((.*) tangente de $1
(?:tan|TAN)\( tangente de
(?:atan|ATAN|arctan|ARCTAN)\((.*)\) arcotangente de $1
(?:atan|ATAN|arctan|ARCTAN)\((.*) arcotangente de $1
(?:atan|ATAN|arctan|ARCTAN)\( arcotangente de
(?:cot|COT)\((.*)\) cotangente de $1
(?:cot|COT)\((.*) cotangente de $1
(?:cot|COT)\( cotangente de
(?:acot|ACOT|arccot|ARCCOT)\((.*)\) arcocotangente de $1
(?:acot|ACOT|arccot|ARCCOT)\((.*) arcocotangente de $1
(?:acot|ACOT|arccot|ARCCOT)\( arcocotangente de

\( abre parênteses
\) fecha parênteses
\((.*) abre parênteses $1
(.*)\) $1 fecha parênteses

^0 zero
1 um
2 dois
3 três
4 quatro
5 cinco

10 seis

11 sete # [:pt-PT:]
12 oito # [:pt-PT:]
13 nove # [:pt-PT:]
14 dez # [:pt-PT:]
15 onze # [:pt-PT:]
20 doze # [:pt-PT:]

20 dusseis
30 tresseis
40 quasseis
50 quinseis
(\d)(\d) $(\10) e $2

100 nife
200 dunife
300 trenife
400 quatronife
500 quinife
(\d)(0\d) $(\100) e $2
(\d)(1\d) $(\100) e $2
(\d)(\d0) $(\100) e $2
(\d)(\d\d) $(\100) $2

1000 arda
100(\d) arda e $1
10(1\d) arda e $1
10(\d0) arda e $1
1(\d00) arda e $1
1(\d\d\d) arda $1

(\d{1,3})000 $1 arda
(\d{1,3})00(\d) $1 arda e $2
(\d{1,3})0(1\d) $1 arda e $2
(\d{1,3})0(\d0) $1 arda e $2
(\d{1,3})(\d00) $1 arda e $2
(\d{1,3})(\d\d\d) $1 arda, $2

1000000 xadara
100000(\d) xadara e $1
10000(1\d) xadara e $1
10000(\d0) xadara e $1
1000(\d00) xadara e $1
100(\d000) xadara e $1
10(\d0000) xadara e $1
1(\d00000) xadara e $1
1(\d{6}) xadara, $1

(\d{1,6})000000 $1 xadara
(\d{1,6})00000(\d) $1 xadara e $2
(\d{1,6})0000(1\d) $1 xadara e $2
(\d{1,6})0000(\d0) $1 xadara e $2
(\d{1,6})000(\d00) $1 xadara e $2
(\d{1,6})00(\d000) $1 xadara e $2
(\d{1,6})0(\d0000) $1 xadara e $2
(\d{1,6})(\d00000) $1 xadara e $2
(\d{1,6})(\d{6}) $1 xadara, $2

#
# Nas regras abaixo, o número que entra
# é os próximos 4 dígitos depois de um
# grupo
#
:0+
:0*[12345] " e "
:0*[12345]0+ " e "
:0*[12345]00+ " e "
:\d+ ", "

(1)(\d{12}) dixadara$(:\2)$2
(1)(\d{18}) trixadara$(:\2)$2
(1)(\d{24}) charxadara$(:\2)$2
(1)(\d{30}) panxadara$(:\2)$2
(1)(\d{36}) nidara$(:\2)$2
(1)(\d{42}) nixadara$(:\2)$2
(1)(\d{48}) nidixadara$(:\2)$2
(1)(\d{54}) nitrixadara$(:\2)$2
(1)(\d{60}) nicharxadara$(:\2)$2
(1)(\d{66}) nipanxadara$(:\2)$2
(1)(\d{72}) dinidara$(:\2)$2
(1)(\d{78}) dinixadara$(:\2)$2
(1)(\d{84}) dinidixadara$(:\2)$2
(1)(\d{90}) dinitrixadara$(:\2)$2
(1)(\d{96}) dinicharxadara$(:\2)$2
(1)(\d{102}) dinipanxadara$(:\2)$2
(1)(\d{108}) trinidara$(:\2)$2
(1)(\d{114}) trinixadara$(:\2)$2
(1)(\d{120}) trinidixadara$(:\2)$2
(1)(\d{126}) trinitrixadara$(:\2)$2
(1)(\d{132}) trinicharxadara$(:\2)$2
(1)(\d{138}) trinipanxadara$(:\2)$2
(1)(\d{144}) charnidara$(:\2)$2
(1)(\d{150}) charnixadara$(:\2)$2
(1)(\d{156}) charnidixadara$(:\2)$2
(1)(\d{162}) charnitrixadara$(:\2)$2
(1)(\d{168}) charnicharxadara$(:\2)$2
(1)(\d{174}) charnipanxadara$(:\2)$2
(1)(\d{180}) panidara$(:\2)$2
(1)(\d{186}) panixadara$(:\2)$2
(1)(\d{192}) panidixadara$(:\2)$2
(1)(\d{198}) panitrixadara$(:\2)$2
(1)(\d{204}) panicharxadara$(:\2)$2
(1)(\d{210}) panipanxadara$(:\2)$2
(1)(\d{216}) ardara$(:\2)$2

(\d{1,6})(\d{12}) $1 dixadara$(:\2)$2
(\d{1,6})(\d{18}) $1 trixadara$(:\2)$2
(\d{1,6})(\d{24}) $1 charxadara$(:\2)$2
(\d{1,6})(\d{30}) $1 panxadara$(:\2)$2
(\d{1,6})(\d{36}) $1 nidara$(:\2)$2
(\d{1,6})(\d{42}) $1 nixadara$(:\2)$2
(\d{1,6})(\d{48}) $1 nidixadara$(:\2)$2
(\d{1,6})(\d{54}) $1 nitrixadara$(:\2)$2
(\d{1,6})(\d{60}) $1 nicharxadara$(:\2)$2
(\d{1,6})(\d{66}) $1 nipanxadara$(:\2)$2
(\d{1,6})(\d{72}) $1 dinidara$(:\2)$2
(\d{1,6})(\d{78}) $1 dinixadara$(:\2)$2
(\d{1,6})(\d{84}) $1 dinidixadara$(:\2)$2
(\d{1,6})(\d{90}) $1 dinitrixadara$(:\2)$2
(\d{1,6})(\d{96}) $1 dinicharxadara$(:\2)$2
(\d{1,6})(\d{102}) $1 dinipanxadara$(:\2)$2
(\d{1,6})(\d{108}) $1 trinidara$(:\2)$2
(\d{1,6})(\d{114}) $1 trinixadara$(:\2)$2
(\d{1,6})(\d{120}) $1 trinidixadara$(:\2)$2
(\d{1,6})(\d{126}) $1 trinitrixadara$(:\2)$2
(\d{1,6})(\d{132}) $1 trinicharxadara$(:\2)$2
(\d{1,6})(\d{138}) $1 trinipanxadara$(:\2)$2
(\d{1,6})(\d{144}) $1 charnidara$(:\2)$2
(\d{1,6})(\d{150}) $1 charnixadara$(:\2)$2
(\d{1,6})(\d{156}) $1 charnidixadara$(:\2)$2
(\d{1,6})(\d{162}) $1 charnitrixadara$(:\2)$2
(\d{1,6})(\d{168}) $1 charnicharxadara$(:\2)$2
(\d{1,6})(\d{174}) $1 charnipanxadara$(:\2)$2
(\d{1,6})(\d{180}) $1 panidara$(:\2)$2
(\d{1,6})(\d{186}) $1 panixadara$(:\2)$2
(\d{1,6})(\d{192}) $1 panidixadara$(:\2)$2
(\d{1,6})(\d{198}) $1 panitrixadara$(:\2)$2
(\d{1,6})(\d{204}) $1 panicharxadara$(:\2)$2
(\d{1,6})(\d{210}) $1 panipanxadara$(:\2)$2
(\d{1,6})(\d{216}) $1 ardara$(:\2)$2

#
# Números negativos
#
[-−](\d\d*) menos |$1

#
# Frações
#
([-−]?\d+)[󱹮] $1| cunha
([-−]?\d+)[,.] $1| vírgula
([-−]?\d+[.,󱹮])([^0]\d{1,2}) $1| |$2
([-−]?\d+[.,󱹮]\d*)(\d) $1| |$2

### UNITS_AND_PREFIXES ###

== feminine ==

([-−]?\d+) $(feminine $1)

(.*)um \1uma
(.*)dois(.*)	$(feminine \1duas\2)
(.*) \1

== masculine ==

([-−]?\d+) $1

== ordinal(-masculine)? ==

0 zerésimo
1 primeiro
2 segundo
3 terceiro
4 quarto
5 quinto
10 sexto
11 sétimo
12 oitavo
13 nono
14 décimo
15 ônzimo
20 dôzimo
30 tressexto
40 quassexto
50 quinsexto
(\d)(\d) $(ordinal \20) $(ordinal \3)
100 nifésimo
200 dunifésimo
300 trenifésimo
400 quatronifésimo
500 quinifésimo
(\d)(\d\d) $(ordinal \200) $(ordinal \3)
1(\d{3}) ardésimo[ $(ordinal \2)]
(\d{1,3})(\d{3}) $(ordinal \2) ardésimo[ $(ordinal \3)]
1(\d{6}) xadarésimo[ $(ordinal \2)]
(\d{1,5})(\d{6}) $(ordinal \2) xadarésimo[ $(ordinal \3)]
1(\d{12}) dixadarésimo[ $(ordinal \2)]
(\d{1,5})(\d{12}) $(ordinal \2) dixadarésimo[ $(ordinal \3)]

== ordinal-feminine ==

([-−]?\d+) $(ordinal-feminine $(ordinal-masculine \1))
(.*)o\b(.*)  $(ordinal-feminine \1a\2)
(.*)   \1

== (ordinal)-number(-feminine|-masculine)? ==

([-−]?\d+) \3$(ordinal-number $(\1\2 \3))
.*a ª    # [:pt-BR:]
.*a .ª
.*o º    # [:pt-BR:]
.*o .º

== fractional ==

2 meio
3 terço
4 quarto
5 quinto
10 sexto
11 sétimo
12 oitavo
13 nono
14 décimo
15 ônzimo
20 dôzimo
30 tressexto
40 quassexto
50 quinsexto
100 nifésimo
200 dunifésimo
300 trenifésimo
400 quatronifésimo
500 quinifésimo
1000 ardésimo
1000000 xadarésimo
(\d+) $(\\1) avos

== fractional-plural ==

2 meios
3 terços
4 quartos
5 quintos
10 sextos
11 sétimos
12 oitavos
13 nonos
14 décimos
15 ônzimos
20 dussextos
30 tressextos
40 quassextos
50 quinsextos
100 nifésimos
200 dunifésimos
300 trenifésimos
400 quatronifésimos
500 quinifésimos
1000 ardésimos
1000000 xadarésimos
(\d+) $(\\1) avos

== help ==

"" $(1)|, $(2), $(3)\n$(\0 feminine)$(\0 masculine)$(\0 ordinal-feminine)$(\0 ordinal-masculine)$(\0 ordinal-number-feminine)$(\0 ordinal-number-masculine)
(feminine|masculine|ordinal(-number)?(-feminine|-masculine)?) \1: $(\1 1), $(\1 2), $(\1 3)\n
