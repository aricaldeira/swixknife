\+ plus
\- minus
− minus
\^ elevated to
\*\* elevated to
\* times
× times
/ divided by
÷ divided by
([-−]?1)[/⁄]2 $1 half
([-−]?\d+)[/⁄]2 $1 halves
([-−]?1)[/⁄](\d+) $1 $(ordinal \\2)
([-−]?\d+)[/⁄](\d+) $1 $(ordinal \\2)s
([-−]?\d+|[-−]?[πτφ])[/⁄] $1 over
([-−]?\d+|[-−]?[πτφ])[/⁄]([-−]?\d+|[-−]?[πτφ]) $1 over $2
⁄ over
mod modulo
([-−]?\d+|[-−]?[πτφ])! $1 factorial
[.,󱹮]{2}|[‥„󱹯] repeating
e e
π pi
τ tau
φ phi
ln\((.*)\) natural logarithm of $1
ln\((.*) natural logarithm of $1
ln\( natural logarithm of
lsez\((.*)\) sezimal logarithm of $1
lsez\((.*) sezimal logarithm of $1
lsez\( sezimal logarithm of
ldec\((.*)\) decimal logarithm of $1
ldec\((.*) decimal logarithm of $1
ldec\( decimal logarithm of
sqrt\((.*)\) square root of $1
sqrt\((.*) square root of $1
sqrt\( square root of
cbrt\((.*)\) cube root of $1
cbrt\((.*) cube root of $1
cbrt\( cube root of
\u200b?² squared
\u200b?³ cubed
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?² $1 squared
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?³ $1 cubed
(?:sin|SIN)\((.*)\) sine of $1
(?:sin|SIN)\((.*) sine of $1
(?:sin|SIN)\( sine of
(?:asin|ASIN|arcsin|ARCSIN)\((.*)\) arcsine of $1
(?:asin|ASIN|arcsin|ARCSIN)\((.*) arcsine of $1
(?:asin|ASIN|arcsin|ARCSIN)\( arcsine of
(?:csc|CSC)\((.*)\) cosecant of $1
(?:csc|CSC)\((.*) cosecant of $1
(?:csc|CSC)\( cosecant of
(?:acsc|ACSC|arccsc|ARCCSC)\((.*)\) arccosecant of $1
(?:acsc|ACSC|arccsc|ARCCSC)\((.*) arccosecant of $1
(?:acsc|ACSC|arccsc|ARCCSC)\( arccosecant of
(?:cos|COS)\((.*)\) cosine of $1
(?:cos|COS)\((.*) cosine of $1
(?:cos|COS)\( cosine of
(?:acos|ACOS|arccos|ARCCOS)\((.*)\) arccosine of $1
(?:acos|ACOS|arccos|ARCCOS)\((.*) arccosine of $1
(?:acos|ACOS|arccos|ARCCOS)\( arccosine of
(?:sec|SEC)\((.*)\) secant of $1
(?:sec|SEC)\((.*) secant of $1
(?:sec|SEC)\( secant of
(?:asec|ASEC|arcsec|ARCSEC)\((.*)\) arcsecant of $1
(?:asec|ASEC|arcsec|ARCSEC)\((.*) arcsecant of $1
(?:asec|ASEC|arcsec|ARCSEC)\( arcsecant of
(?:tan|TAN)\((.*)\) tangent of $1
(?:tan|TAN)\((.*) tangent of $1
(?:tan|TAN)\( tangent of
(?:atan|ATAN|arctan|ARCTAN)\((.*)\) arctangent of $1
(?:atan|ATAN|arctan|ARCTAN)\((.*) arctangent of $1
(?:atan|ATAN|arctan|ARCTAN)\( arctangent of
(?:cot|COT)\((.*)\) cotangent of $1
(?:cot|COT)\((.*) cotangent of $1
(?:cot|COT)\( cotangent of
(?:acot|ACOT|arccot|ARCCOT)\((.*)\) arccotangent of $1
(?:acot|ACOT|arccot|ARCCOT)\((.*) arccotangent of $1
(?:acot|ACOT|arccot|ARCCOT)\( arccotangent of

\( open parenthesis
\) close parenthesis
\((.*) open parenthesis $1
(.*)\) $1 close parenthesis

^0 zero
1 one
2 two
3 three
4 four
5 five
10 six
11 seven
12 eight
13 nine
14 ten
15 eleven
20 twelve

2(\d) dozen-$1
30 thirsy
40 foursy
50 fifsy
(\d)(\d) $(\10)-$2

100 nif
1(1\d) nif and $1
1(\d0) nif and $1
1(\d\d) nif $1

(\d)00 $1 nif
(\d)(1\d) $1 nif and $2
(\d)(\d0) $1 nif and $2
(\d)(\d\d) $1 nif $2

1000 arda
100(\d) arda and $1
10(1\d) arda and $1
10(\d0) arda and $1
1(\d00) arda and $1
1(\d\d\d) arda $1

(\d{1,3})000 $1 arda
(\d{1,3})00(\d) $1 arda and $2
(\d{1,3})0(1\d) $1 arda and $2
(\d{1,3})0(\d0) $1 arda and $2
(\d{1,3})(\d00) $1 arda and $2
(\d{1,3})(\d\d\d) $1 arda, $2

1000000 shadara
100000(\d) shadara and $1
10000(1\d) shadara and $1
10000(\d0) shadara and $1
1000(\d00) shadara and $1
100(\d000) shadara and $1
10(\d0000) shadara and $1
1(\d00000) shadara and $1
1(\d{6}) shadara, $1

(\d{1,6})000000 $1 shadara
(\d{1,6})00000(\d) $1 shadara and $2
(\d{1,6})0000(1\d) $1 shadara and $2
(\d{1,6})0000(\d0) $1 shadara and $2
(\d{1,6})000(\d00) $1 shadara and $2
(\d{1,6})00(\d000) $1 shadara and $2
(\d{1,6})0(\d0000) $1 shadara and $2
(\d{1,6})(\d00000) $1 shadara and $2
(\d{1,6})(\d{6}) $1 shadara, $2

# separator function
:0+			# one million
:0*\d?\d " and "	# one million and twenty-two
:\d+ ", "		# one million, one thousand

(1)(\d{12}) dishadara$(:\2)$2
(1)(\d{18}) trishadara$(:\2)$2
(1)(\d{24}) charshadara$(:\2)$2
(1)(\d{30}) panshadara$(:\2)$2
(1)(\d{36}) shashadara$(:\2)$2
(1)(\d{42}) shaekashadara$(:\2)$2
(1)(\d{48}) shadishadara$(:\2)$2
(1)(\d{54}) shatrishadara$(:\2)$2
(1)(\d{60}) shacharshadara$(:\2)$2
(1)(\d{66}) shapanshadara$(:\2)$2
(1)(\d{72}) dishashadara$(:\2)$2

(\d{1,6})(\d{12}) $1 dishadara$(:\2)$2
(\d{1,6})(\d{18}) $1 trishadara$(:\2)$2
(\d{1,6})(\d{24}) $1 charshadara$(:\2)$2
(\d{1,6})(\d{30}) $1 panshadara$(:\2)$2
(\d{1,6})(\d{36}) $1 shashadara$(:\2)$2
(\d{1,6})(\d{42}) $1 shaekashadara$(:\2)$2
(\d{1,6})(\d{48}) $1 shadishadara$(:\2)$2
(\d{1,6})(\d{54}) $1 shatrishadara$(:\2)$2
(\d{1,6})(\d{60}) $1 shacharshadara$(:\2)$2
(\d{1,6})(\d{66}) $1 shapanshadara$(:\2)$2
(\d{1,6})(\d{72}) $1 dishashadara$(:\2)$2

# negative number

[-−](\d+) negative |$1

# decimals

([-−]?\d+)[,.󱹮] $1| point
([-−]?\d+[.,󱹮])([^0]\d{1,2}) $1| |$2
([-−]?\d+[.,󱹮]\d*)(\d) $1| |$2

### UNITS_AND_PREFIXES ###

== money ==

"(JPY [-−]?\d+)[.,](\d\d)0" $1
"(JPY [-−]?\d+[.,]\d\d)(\d)" $1 $2 rin

"([A-Z]{3}) ([-−]?1)([.,]00?)?" $2$(\1:us)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $2$(\1:up)

"(CNY [-−]?\d+)[.,](\d)0?" $1 $2 jiao
"(CNY [-−]?\d+[.,]\d)(\d)" $1 $2 fen

"(MGA|MRO) ([-−]?\d+)[.,]0" $2$(\1:us)
"(MGA|MRO) ([-−]?\d+)[.,]2" $2 and 1/5$(\1:us)
"(MGA|MRO) ([-−]?\d+)[.,]4" $2 and 2/5$(\1:up)
"(MGA|MRO) ([-−]?\d+)[.,]6" $2 and 3/5$(\1:up)
"(MGA|MRO) ([-−]?\d+)[.,]8" $2 and 4/5$(\1:up)

"([A-Z]{3}) ([-−]?1)" $2$(\1:us)
"([A-Z]{3}) ([-−]?\d+)" $2$(\1:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](01)" $3 and 1/100$(\2:us)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d)" $3 and \40/100$(\2:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d\d)" $3 and \4/100$(\2:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d\d\d)" $3 and \4/1000$(\2:up)

== ordinal ==

# convert to text, and recall to convert
# cardinal names to ordinal ones

([-−]?\d+) $(ordinal |$1)

(.*)one	\1first
(.*)two	\1second
(.*)three	\1third
(.*)five	\1fifth
(.*)eight	\1eighth
(.*)nine	\1ninth
(.*)twelve	\1twelfth
(.*)y	\1ieth
(.*)	\1th

== ordinal-number ==

(.*1\d)	\1th
(.*1)	\1st
(.*2)	\1nd
(.*3)	\1rd
(.*)	\1th

== year ==

(1[1-9])00 $1 hundred
(1[1-9])([0-9][0-9]) $1 $2
(.*) $(year-remove-and $1)

== year-remove-and ==

"(.*) and (.*)" \1 \2
(.*) \1

== help ==

"" $(1)|, $(2), $(3)\n$(\0 ordinal)$(\0 ordinal-number)year: $(year 1999), two thousand, $(year 2001)\ncurrency \(for example, USD\): $(USD 2.5)\nmoney USD: $(money USD 2.5)
"(ordinal(-number)?|USD)" \1: $(\1 1), $(\1 2), $(\1 3)\n

