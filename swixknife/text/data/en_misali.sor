\+ plus
\- minus
− minus
\^ elevated to
\*\* elevated to
\* times
× times
/ divided by
÷ divided by
([-−]?1)[/⁄]2 $1 half
([-−]?\d+)[/⁄]2 $1 halves
([-−]?1)[/⁄]4 $1 quarter
([-−]?\d+)[/⁄]4 $1 quarters
([-−]?1)[/⁄](\d+) $1 $(ordinal \\2)
([-−]?\d+)[/⁄](\d+) $1 $(ordinal \\2)s
([-−]?\d+|[-−]?[πτφ])[/⁄] $1 over
([-−]?\d+|[-−]?[πτφ])[/⁄]([-−]?\d+|[-−]?[πτφ]) $1 over $2
⁄ over
mod modulo
([-−]?\d+|[-−]?[πτφ])! $1 factorial
[.,󱹮]{2}|[‥„󱹯] repeating
([-−]?\d+[,.]?\d*)% $1 per nif
% per nif
([-−]?\d+[,.]?\d*)‰ $1 per six nif
‰ six nif
([-−]?\d+[,.]?\d*)‱ $1 per unexian
‱ per unexian
e e
π pi
τ tau
φ phi
\( open parenthesis
\) close parenthesis
ln\((.*)\) natural logarithm of $1
ln\((.*) natural logarithm of $1
ln\( natural logarithm of
lsez\((.*)\) sezimal logarithm of $1
lsez\((.*) sezimal logarithm of $1
lsez\( sezimal logarithm of
ldec\((.*)\) decimal logarithm of $1
ldec\((.*) decimal logarithm of $1
ldec\( decimal logarithm of
sqrt\((.*)\) square root of $1
sqrt\((.*) square root of $1
sqrt\( square root of
cbrt\((.*)\) cube root of $1
cbrt\((.*) cube root of $1
cbrt\( cube root of
\u200b?² squared
\u200b?³ cubed
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?² $1 squared
([-−]?\d+|[-−]?\d+[.,󱹮]\d*|[eπτφ])\u200b?³ $1 cubed
(?:sin|SIN)\((.*)\) sine of $1
(?:sin|SIN)\((.*) sine of $1
(?:sin|SIN)\( sine of
(?:asin|ASIN|arcsin|ARCSIN)\((.*)\) arcsine of $1
(?:asin|ASIN|arcsin|ARCSIN)\((.*) arcsine of $1
(?:asin|ASIN|arcsin|ARCSIN)\( arcsine of
(?:csc|CSC)\((.*)\) cosecant of $1
(?:csc|CSC)\((.*) cosecant of $1
(?:csc|CSC)\( cosecant of
(?:acsc|ACSC|arccsc|ARCCSC)\((.*)\) arccosecant of $1
(?:acsc|ACSC|arccsc|ARCCSC)\((.*) arccosecant of $1
(?:acsc|ACSC|arccsc|ARCCSC)\( arccosecant of
(?:cos|COS)\((.*)\) cosine of $1
(?:cos|COS)\((.*) cosine of $1
(?:cos|COS)\( cosine of
(?:acos|ACOS|arccos|ARCCOS)\((.*)\) arccosine of $1
(?:acos|ACOS|arccos|ARCCOS)\((.*) arccosine of $1
(?:acos|ACOS|arccos|ARCCOS)\( arccosine of
(?:sec|SEC)\((.*)\) secant of $1
(?:sec|SEC)\((.*) secant of $1
(?:sec|SEC)\( secant of
(?:asec|ASEC|arcsec|ARCSEC)\((.*)\) arcsecant of $1
(?:asec|ASEC|arcsec|ARCSEC)\((.*) arcsecant of $1
(?:asec|ASEC|arcsec|ARCSEC)\( arcsecant of
(?:tan|TAN)\((.*)\) tangent of $1
(?:tan|TAN)\((.*) tangent of $1
(?:tan|TAN)\( tangent of
(?:atan|ATAN|arctan|ARCTAN)\((.*)\) arctangent of $1
(?:atan|ATAN|arctan|ARCTAN)\((.*) arctangent of $1
(?:atan|ATAN|arctan|ARCTAN)\( arctangent of
(?:cot|COT)\((.*)\) cotangent of $1
(?:cot|COT)\((.*) cotangent of $1
(?:cot|COT)\( cotangent of
(?:acot|ACOT|arccot|ARCCOT)\((.*)\) arccotangent of $1
(?:acot|ACOT|arccot|ARCCOT)\((.*) arccotangent of $1
(?:acot|ACOT|arccot|ARCCOT)\( arccotangent of

\( open parenthesis
\) close parenthesis
\((.*) open parenthesis $1
(.*)\) $1 close parenthesis

^0 zero
1 one
2 two
3 three
4 four
5 five
10 six
11 seven
12 eight
13 nine
14 ten
15 eleven
20 twelve

2(\d) dozen $1 # [:en-US:]
2(\d) dozen-$1
30 thirsy
40 foursy
50 fifsy
(\d)(\d) $(\10) $2 # [:en-US:]
(\d)(\d) $(\10)-$2

100 nif
1(1\d) nif and $1
1(\d0) nif and $1
1(\d\d) nif $1

(\d{1,2})00 $1 nif
(\d{1,2})(1\d) $1 nif and $2
(\d{1,2})(\d0) $1 nif and $2
(\d{1,2})(\d\d) $1 nif $2

# separator function
:0+			# one million
:0*\d?\d " and "	# one million and twenty-two
:\d+ ", "		# one million, one thousand

(\d{1,4})(\d{4}) $1 unexian$(:\2)$2
(\d{1,4})(\d{8})  $1 biexian$(:\2)$2
(\d{1,4})(\d{12}) $1 triexian$(:\2)$2
(\d{1,4})(\d{16}) $1 quadexian$(:\2)$2
(\d{1,4})(\d{20}) $1 pentexian$(:\2)$2
(\d{1,4})(\d{24}) $1 unnilexian$(:\2)$2
(\d{1,4})(\d{28}) $1 ununexian$(:\2)$2
(\d{1,4})(\d{32}) $1 unbiexian$(:\2)$2
(\d{1,4})(\d{36}) $1 untriexian$(:\2)$2
(\d{1,4})(\d{40}) $1 unquadexian$(:\2)$2
(\d{1,4})(\d{44}) $1 unpentexian$(:\2)$2
(\d{1,4})(\d{48}) $1 binilexian$(:\2)$2

# negative number

[-−](\d+) negative |$1

# decimals

([-−]?\d+)[󱹮] $1| and then
([-−]?\d+)[,.] $1| point
([-−]?\d+[.,󱹮])([^0]\d{1,3}) $1| |$2
([-−]?\d+[.,󱹮]\d*)(\d) $1| |$2

### UNITS_AND_PREFIXES ###

== money ==

"(JPY [-−]?\d+)[.,](\d\d)0" $1
"(JPY [-−]?\d+[.,]\d\d)(\d)" $1 $2 rin

"([A-Z]{3}) ([-−]?1)([.,]00?)?" $2$(\1:us)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $2$(\1:up)

"(CNY [-−]?\d+)[.,](\d)0?" $1 $2 jiao
"(CNY [-−]?\d+[.,]\d)(\d)" $1 $2 fen

"(MGA|MRO) ([-−]?\d+)[.,]0" $2$(\1:us)
"(MGA|MRO) ([-−]?\d+)[.,]2" $2 and 1/5$(\1:us)
"(MGA|MRO) ([-−]?\d+)[.,]4" $2 and 2/5$(\1:up)
"(MGA|MRO) ([-−]?\d+)[.,]6" $2 and 3/5$(\1:up)
"(MGA|MRO) ([-−]?\d+)[.,]8" $2 and 4/5$(\1:up)

"([A-Z]{3}) ([-−]?1)" $2$(\1:us)
"([A-Z]{3}) ([-−]?\d+)" $2$(\1:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](01)" $3 and 1/100$(\2:us)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d)" $3 and \40/100$(\2:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d\d)" $3 and \4/100$(\2:up)
"(([A-Z]{3}) ([-−]?\d+))[.,](\d\d\d)" $3 and \4/1000$(\2:up)

== ordinal ==

# convert to text, and recall to convert
# cardinal names to ordinal ones

([-−]?\d+) $(ordinal |$1)

(.*)one	\1first
(.*)two	\1second
(.*)three	\1third
(.*)five	\1fifth
(.*)eight	\1eighth
(.*)nine	\1ninth
(.*)twelve	\1twelfth
(.*)y	\1ieth
(.*)	\1th

== ordinal-number ==

(.*1\d)	\1th
(.*1)	\1st
(.*2)	\1nd
(.*3)	\1rd
(.*)	\1th

== year ==

(1[1-9])00 $1 hundred
(1[1-9])([0-9][0-9]) $1 $2
(.*) $(year-remove-and $1)

== year-remove-and ==

"(.*) and (.*)" \1 \2
(.*) \1

== help ==

"" $(1)|, $(2), $(3)\n$(\0 ordinal)$(\0 ordinal-number)year: $(year 1999), two thousand, $(year 2001)\ncurrency \(for example, USD\): $(USD 2.5)\nmoney USD: $(money USD 2.5)
"(ordinal(-number)?|USD)" \1: $(\1 1), $(\1 2), $(\1 3)\n

